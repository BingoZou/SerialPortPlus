apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'io.netty:netty-buffer:4.1.51.Final'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = rootProject.ext.versionName//version
group = "com.azhon"//groupId
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'azhon'//创建的仓库名字
        name = 'serialPortPlus'//packageName
        websiteUrl = rootProject.ext.siteUrl
        vcsUrl = rootProject.ext.gitUrl
        licenses = ['Apache-2.0']//创建仓库时选择的license type
        userOrg = 'azhon' //自己bintray创建的organization名称
        publish = true // 是否是公开项目。

        version {
            name = rootProject.ext.versionName//版本号
            desc = rootProject.ext.versionDesc//版本描述
            released = new Date()
            vcsTag = "v$rootProject.ext.versionName"
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                //项目描述
                description 'Android串口开发，基于官方的android-serialport-api增加支持设置奇偶校验、数据位和停止位'
                url siteUrl
                // 填写开源协议
                licenses {
                    license {
                        name 'Apache-2.0'
                        url ' http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'azhon'
                        //填写邮件地址
                        email 'azhon.cn@gmail.com'
                    }
                }
                scm {
                    connection rootProject.ext.gitUrl
                    developerConnection rootProject.ext.gitUrl
                    url siteUrl
                }
            }
        }
    }
}